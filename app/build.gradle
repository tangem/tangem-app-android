apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.tangem.wallet"
        versionCode project.hasProperty('versionCode') ? project.property('versionCode') as int : 1
        versionName project.hasProperty('versionName') ? project.property('versionName') : "1.0.0.-SNAPSHOT"
        minSdkVersion 21
        targetSdkVersion 29
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    androidExtensions {
        experimental = true
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            buildConfigField 'String', 'CONFIG_ENVIRONMENT', '\"prod\"'
        }
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.debug
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
            buildConfigField 'String', 'CONFIG_ENVIRONMENT', '\"dev\"'
        }
        debug_beta {
            initWith debug
            versionNameSuffix "-beta"
            applicationIdSuffix ".debug"
            buildConfigField 'String', 'CONFIG_ENVIRONMENT', '\"prod\"'
        }
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    packagingOptions {
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'lib/x86_64/freebsd/libscrypt.so'
        exclude 'lib/x86_64/linux/libscrypt.so'
    }
    buildToolsVersion '29.0.3'
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.2.1'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

    implementation 'com.tangem:blockchain:1.141.0'
    implementation 'com.tangem:core:1.101.0'
    implementation 'com.tangem:sdk:1.101.0'

    // WebView
    implementation "androidx.browser:browser:1.3.0"

    //lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"

    implementation "org.rekotlin:rekotlin:1.0.4"

    // QR
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'

    // Camera
    implementation 'com.otaliastudios:cameraview:2.6.3'

    // Image Loading - Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:26.0.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    testImplementation 'junit:junit:4.13.1'
    testImplementation "com.google.truth:truth:1.0.1"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}